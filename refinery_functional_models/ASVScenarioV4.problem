enum VesselType {OtherType, CargoShip, Tanker,
                 ContainerShip, PassengerShip,
                 FishingShip, MotorVessel,
                 SailingVessel, MilitaryVessel}

enum StaticObstacleType {Small, Medium, Large}

abstract class Vessel {
    VesselType[1] vesselType
    Vessel[0..*] overtaking
    Vessel[0..*] crossingFromPort
    Vessel[0..*] headOn opposite headOn
}

class OS extends Vessel.
class TS extends Vessel.

class StaticObstacle {
    StaticObstacleType[1] staticObstacleType
    OS[0..1] inPortSideSector
    OS[0..1] inStarboardSideSector
    OS[0..1] inSternSector
    OS[0..1] inHeadOnSector
}

pred inMastheadSector(StaticObstacle o, OS os) <->
    inPortSideSector(o, os), inSternSector(o, os).

pred giveWay(Vessel v1) <-> 
    overtaking(v1, _);
    crossingFromPort(v1, _);
    headOn(_, v1).

pred standOn(Vessel v1) <->
    overtaking(_, v1);
    crossingFromPort(_, v1).

pred ambiguous(Vessel v) <->
    giveWay(v), standOn(v).

pred inCOLREGSSituationWith(Vessel v1, Vessel v2) <->
    headOn(v2, v1);
    crossingFromPort(v1, v2);
    crossingFromPort(v2, v1);
    overtaking(v1, v2);
    overtaking(v2, v1).

pred inSituationWith(StaticObstacle o, OS os) <->
    inMastheadSector(o, os);
    inSternSector(o, os).

//------------------------------------------------------------------------

error selfCOLREGSRelation(Vessel v) <->
    overtaking(v, v);
    crossingFromPort(v, v);
    headOn(v, v).


error doubleCOLREGSRelation(Vessel v1, Vessel v2) <->
    headOn(v1, v2), crossingFromPort(v1, v2);
    headOn(v1, v2), overtaking(v1, v2);
    overtaking(v1, v2), crossingFromPort(v1, v2);

    headOn(v1, v2), crossingFromPort(v2, v1);
    headOn(v1, v2), overtaking(v2, v1);
    overtaking(v1, v2), crossingFromPort(v2, v1);

    crossingFromPort(v1, v2), crossingFromPort(v2, v1);
    overtaking(v1, v2), overtaking(v2, v1).


error doubleSectorRelation(StaticObstacle o, OS os) <->
    inSternSector(o, os), inMastheadSector(o, os);
    inPortSideSector(o, os), inStarboardSideSector(o, os);
    inHeadOnSector(o, os), !inMastheadSector(o, os).

//----------------------------------------------------------------
error notInCOLREGSSituationWithOS(TS ts) <->
    !inCOLREGSSituationWith(ts, os), OS(os).

error twoTSInCOLREGSSituation(TS ts1, TS ts2) <->
    inCOLREGSSituationWith(ts1, ts2).

error notInSituationWithOS(StaticObstacle o) <->
    !inSituationWith(o, os), OS(os).

error moreThanOneOS(OS v1, OS v2) <->
    v1 != v2.

error noOS() <->
    !OS(_).


OS(OS).
vesselType(OS, VesselType::PassengerShip).

TS(TS1).
TS(TS2).
TS(TS3).
TS(TS4).
TS(TS5).
scope OS = 1, StaticObstacle=3, TS=5.
